breed [workers worker]
workers-own [my_mailbox current_task worker-thread poisened?]

;; creates a new worker and reports it
to-report new-worker
  let w nobody
  let mbox new-mailbox
  create-workers 1 [
    set w self
    set my_mailbox mbox
    set worker-thread nobody ;; initially a worker has no thread to work with
    set current_task nobody ;; initially a worker has no currently executed task
    set poisened? false
    hide-turtle
  ]
  print "created a new Worker"
  report w
end

;; sends the given task to the given worker
to send-to-worker [w task]
  ask w [
     enqueue-task my_mailbox task
  ]
end

;; reports the size of the contained mailbox
to-report queue-size? [w]
  let s 0
  ask w [
    set s messages? my_mailbox
  ]
  report s
end

to poisen
  set poisened? true
end

to-report is-poisened? 
  report poisened?
end

to-report can-be-cleared?
  ifelse is-poisened? and current_task = nobody and messages? my_mailbox = 0 [report true] [report false]
end

;; indicates if this worker is budy or not
;; the worker is busy if it has at least the given amount of messages currently procesing
to-report is-busy? [pt]
  ifelse pt <= messages? my_mailbox [report true] [report false]
end

;; checks if this worker needs a thread to run its tasks
;; important: only call this from a worker context
to-report need-thread?
  
  if has-thread? [ report false ]
  if current_task != nobody [ report true ]
  if has-task my_mailbox [ report true ]
  report false
end

;; checks if this worker has a thread
;; important: only call this from a worker context
to-report has-thread? 
  report worker-thread != nobody
end

;; sets the given thread as worker-thread for this worker
;; important: only call this from a worker context
to set-worker-thread [th]
  set worker-thread th
end

;; releases the worker-thread of this worker
;; important: only call this from a worker context
to release-worker-thread
  
  set FreeThreadsList lput worker-thread FreeThreadsList
  set-worker-thread nobody
end

;; the main worker loop to run one tick for the workers
to worker-loop 
  ask workers with [has-thread?] [ ;; call the task processing only if the worker has a thread to run the task on ...
    
    ;; check if we have a task which is processed atm
    if current_task = nobody [
      if has-task my_mailbox [
        set current_task dequeue-task my_mailbox
        ;; print "task geholt"
      ]
    ]
    
    let t current_task
    
    if is-list? t [
      
      ;; print "is-list"
      ifelse empty? t [
        set t nobody
        set current_task nobody
        release-worker-thread
        ;; print "list-task finished"
      ]
      [
        set t item 0 t
      ]
    ]
    
    ;; check that we have something to process
    if is-anonymous-reporter? t [
      
      ;; print "is reporter"
      ;; process now
      let result runresult t
      
      if is-list? result [ ;; taskload task
        set t result
      ]
            
      if is-number? result [ ;; simple task
        set t result
      ]

      if is-boolean? result and result = true [ ;; send-task
        set t nobody
        release-worker-thread
        ;; print "send-task finished"
      ]
    ]
    
    if is-number? t [
      
      ;; print "is-number"
      ifelse t > 0 [
        set t t - 1
      ]
      [
        set t nobody
        release-worker-thread
        ;; print "simple-task finished"
      ]
    ]
    
    if is-list? current_task [
      
      ;; is t processed or not ?
      ifelse  t = nobody [
        set t butfirst current_task
      ]
      [
        set t replace-item 0 current_task t
      ]
    ]
    
    set current_task t
  ]
end
